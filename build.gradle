import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id "com.github.spotbugs" version "2.0.0"
    id 'checkstyle'
    id 'java'
}

def spotbugsVer = '3.1.12'

group = 'com.github.kuro46'
version = '0.2.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
    maven { url "https://jitpack.io" }
}

dependencies {
    // Bukkit
    compileOnly "org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT"
    // SpotBugs Annotations
    compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugsVer}"
    // CommandUtility
    implementation "com.github.kuro46:CommandUtility:493af33"
    // Gson
    implementation 'com.google.code.gson:gson:2.8.5'
    // Guava
    implementation 'com.google.guava:guava:28.1-jre'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

processResources {
    filter { String line ->
        line.replace('$version', version)
    }
}

// Shadow settings

shadowJar {
    minimize()
    archiveFileName = "ScriptBlock-Improved.jar"
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = 'com.github.kuro46.scriptblockimproved.libs'
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

// SpotBugs settings

tasks.withType(com.github.spotbugs.SpotBugsTask) {
  reports.xml.enabled = false
  reports.html.enabled = true
}

spotbugs {
  toolVersion = spotbugsVer
}
