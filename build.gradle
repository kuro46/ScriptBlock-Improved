plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'net.ltgt.errorprone' version '1.1.1'
    id 'io.freefair.lombok' version '5.0.0'
    id 'maven-publish'
    id 'java'
}

group = 'xyz.shirokuro'
version = '2.1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' }
    maven { url 'https://maven.shirokuro.xyz/repos/releases/' }
    maven { url 'https://maven.shirokuro.xyz/repos/snapshots/' }
}

dependencies {
    implementation 'xyz.shirokuro:commandutility:0.6.1-SNAPSHOT'
    // Bukkit
    compileOnly 'org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT'
    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'
    // Guava
    implementation 'com.google.guava:guava:29.0-jre'
    // Apache Commons Lang
    implementation 'org.apache.commons:commons-lang3:3.9'
    // Apache Commons Text
    implementation 'org.apache.commons:commons-text:1.8'
    // Fuzzy Wuzzy
    implementation 'me.xdrop:fuzzywuzzy:1.2.0'
    // Bukkit for test
    testImplementation 'org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT'
    // JUnit
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    // ErrorProne
    errorprone 'com.google.errorprone:error_prone_core:2.+'
}

if (!JavaVersion.current().isJava9Compatible()) {
    dependencies {
        errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

processResources {
    filter { String line ->
        line.replace '$version', version.toString()
    }
}

// Shadow settings

shadowJar {
    minimize()
    archiveFileName = "ScriptBlock-Improved.jar"

    relocate 'com.google', 'xyz.shirokuro.scriptblockimproved.libs.google'
    relocate 'me.xdrop', 'xyz.shirokuro.scriptblockimproved.libs.me.xdrop'
    relocate 'org.apache.commons.lang3', 'xyz.shirokuro.scriptblockimproved.libs.commons.lang'
    relocate 'org.apache.commons.text', 'xyz.shirokuro.scriptblockimproved.libs.commons.text'
    relocate 'org.checkerframework', 'xyz.shirokuro.scriptblockimproved.libs.checkerframework'
    relocate 'xyz.shirokuro.commandutility', 'xyz.shirokuro.scriptblockimproved.libs.commandutility'
}

test {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

// maven

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            from components.java
            pom {
                name = 'ScriptBlock-Improved'
                description = 'A improved version of ScriptBlock'
                url = 'https://github.com/kuro46/ScriptBlock-Improved'
            }
        }
    }
    repositories {
        if (version.endsWith("SNAPSHOT")) {
            mavenLocal()
        } else {
            maven {
                def publishRootDir = project.hasProperty("sbi.publish.rootdir")
                    ? project.getProperty("sbi.publish.rootdir").toString()
                    : buildDir.toString()
                url = "$publishRootDir/repos/releases"
            }
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
