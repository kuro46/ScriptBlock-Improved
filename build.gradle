plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id "net.ltgt.errorprone" version "0.8.1"
    id "com.github.spotbugs" version "2.0.0"
    id 'checkstyle'
    id 'maven'
    id 'java'
}

def spotbugsVer = '3.1.12'

group = 'com.github.kuro46'
version = '0.3.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
}

dependencies {
    // Bukkit
    compileOnly "org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT"
    // SpotBugs Annotations
    compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugsVer}"
    // Gson
    implementation 'com.google.code.gson:gson:2.8.5'
    // Guava
    implementation 'com.google.guava:guava:28.1-jre'
    // ErrorProne
    errorprone("com.google.errorprone:error_prone_core:2.+")
    errorproneJavac("com.google.errorprone:javac:9+181-r4173-1")
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:all" << "-Werror"
}

processResources {
    filter { String line ->
        line.replace('$version', version)
    }
}

// Shadow settings

shadowJar {
    minimize()
    archiveFileName = "ScriptBlock-Improved-${version}.jar"

    relocate 'com.google', 'com.github.kuro46.scriptblockimproved.libs.google'
    relocate 'org.checkerframework', 'com.github.kuro46.scriptblockimproved.libs.checkerframework'
}

artifacts {
    archives shadowJar
}

task pluginJar {
    doFirst {
        shadowJar {
            archiveFileName = "ScriptBlock-Improved.jar"
        }
    }

    finalizedBy "shadowJar"
}

// SpotBugs settings

tasks.withType(com.github.spotbugs.SpotBugsTask) {
  reports.xml.enabled = false
  reports.html.enabled = true
}

spotbugs {
  toolVersion = spotbugsVer
}
